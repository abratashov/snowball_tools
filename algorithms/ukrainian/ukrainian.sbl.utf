stringescapes {}

/* the 33 Ukrainian letters and apostrophe represented by single quote */

stringdef a    '{U+0430}'
stringdef b    '{U+0431}'
stringdef v    '{U+0432}'
stringdef gh   '{U+0433}'
stringdef g    '{U+0491}'
stringdef d    '{U+0434}'
stringdef e    '{U+0435}'
stringdef ye   '{U+0454}'
stringdef zh   '{U+0436}'
stringdef z    '{U+0437}'
stringdef y    '{U+0438}'
stringdef i    '{U+0456}'
stringdef yi   '{U+0457}'
stringdef i`   '{U+0439}'
stringdef k    '{U+043A}'
stringdef l    '{U+043B}'
stringdef m    '{U+043C}'
stringdef n    '{U+043D}'
stringdef o    '{U+043E}'
stringdef p    '{U+043F}'
stringdef r    '{U+0440}'
stringdef s    '{U+0441}'
stringdef t    '{U+0442}'
stringdef u    '{U+0443}'
stringdef f    '{U+0444}'
stringdef kh   '{U+0445}'
stringdef ts   '{U+0446}'
stringdef ch   '{U+0447}'
stringdef sh   '{U+0448}'
stringdef shch '{U+0449}'
stringdef soft '{U+044C}'
stringdef iu   '{U+044E}'
stringdef ia   '{U+044F}'

// Other Cyrillic letters from the Russian alphabet that could happen in the text
stringdef e"   '{U+0451}'
stringdef hard '{U+044A}'
stringdef y`   '{U+044B}'
stringdef e`   '{U+044D}'

// Apostrophe-like symbols
// stringdef a_apostrophe      '{U+0027}' // '
// stringdef a_grave_accent   U+0060   // ` cannot to remove system char in Snowball
stringdef a_ml_prime        '{U+02B9}' // ʹ
stringdef a_mlt_comma       '{U+02BB}' // ʻ
stringdef a_ml_apostrophe   '{U+02BC}' // ʼ
stringdef a_mlr_comma       '{U+02BD}' // ʽ
stringdef a_mlv_line        '{U+02C8}' // ˈ
stringdef a_lsq_mark        '{U+2018}' // ‘
stringdef a_rsq_mark        '{U+2019}' // ’
stringdef a_shr9q_mark      '{U+201B}' // ‛
stringdef a_prime           '{U+2032}' // ′

routines (
  prelude
  mark_regions R2
  min_len
  short_word_4l
  short_word_5l
  long_word
  perfective_gerund
  reflexive
  long_endings
  adjective
  adjectival
  verb
  noun
  // remove_last_letter
  remove_last_2_letters
  remove_last_2_vowels
  remove_vowel_before_vowel
  remove_vowel
  derivational
  remove_letter_v
  tidy_up
)

externals ( stem )

integers ( pV p2 )

groupings ( v )

define v 'аеєиіїоуюяь'

define prelude as (
  do repeat ( goto (['ґ']) <- 'г' )

  // Replace possible Cyrillic letters
  do repeat ( goto (['ё']) <- 'е' )
  do repeat ( goto (['ы']) <- 'и' )
  do repeat ( goto (['э']) <- 'є' )
  do repeat ( goto (['ъ']) delete )

  // Remove any apostrophe-like symbols
  do repeat ( goto (['{'}']) delete )
  do repeat ( goto (['ʹ']) delete )
  do repeat ( goto (['ʻ']) delete )
  do repeat ( goto (['ʼ']) delete )
  do repeat ( goto (['ʽ']) delete )
  do repeat ( goto (['ˈ']) delete )
  do repeat ( goto (['‘']) delete )
  do repeat ( goto (['’']) delete )
  do repeat ( goto (['‛']) delete )
  do repeat ( goto (['′']) delete )
)

define mark_regions as (
  $pV = limit
  $p2 = limit
  do (
    gopast v   setmark pV     gopast non-v
    gopast v   gopast non-v   setmark p2
  )
)

backwardmode (
  define R2 as $p2 <= cursor

  define perfective_gerund as (
    [substring] among (
      'вши'   // написа|вши
      'вшись'
      'вшися'
      'учи'
      'ючи'
      'ючись'
      'ючися'
      'ачи'
      'ачись'
      'ачися'
      'лячи'
      'лячись'
      'лячися'
      'ячи'
      'ячись'
      'ячися'
        (delete)
   )
  )

  define reflexive as (
    [substring] among (
      'ється' // смі|ється
      'еться'
      'ся'
      'сь'
        (delete)
    )
  )

  define long_endings as (
    [substring] among (
      // nouns
      'ість' 'істю' 'істи' 'істе' 'істського'
      'ність' 'ністю' // ?
      'енами'
      'очки' 'очці' 'очку' 'очкою' 'очка'
      'очко' 'очок' 'очкам' 'очками' 'очках'
      'ості' 'ости' 'осте' 'остей' 'остям' 'остями' 'остях'
      'ності' 'ности' 'носте' 'ностей' 'ностям' 'ностями' 'ностях' // ?
      'ником' 'никові' 'нику' 'ників' 'ника' 'никам' 'никами' 'никах' 'ники' // need to remove ?
      // 'ніком' 'нікові' 'ніку' 'ніків' 'ніка' 'нікам' 'ніками' 'ніках' 'ніки' // need to remove ?
      'ців' 'цями' 'цям' 'цях' 'цем' 'цеві' // ? авіанос/цям/цях/ців/цями/цем/цеві
      'ків'
      'нні'

      // female gender
      'кою' 'кам' 'ками' 'ках'
      'ською' 'ська' 'ську' 'сько'
      'очна'
        (delete)
    )
  )

  define adjective as (
    [substring] among (
      'ий' 'ого' 'ому' 'им' 'ім'                 // зел|ен.ий
      'іший' 'ішого' 'ішому' 'ішим' 'ішім' 'іше' 'іш' 'іша' 'ішої' 'ішій' 'ішу' 'ішою' 'іші' 'іших' 'ішими'
      'ніший' 'нішого' 'нішому' 'нішим' 'нішім' 'ніше' 'ніш' 'ніша' 'нішої' 'нішій' 'нішу' 'нішою' 'ніші' 'ніших' 'нішими'
      'ої' 'ій' 'ою'
      'их' 'ими'
      'ний' 'на' 'ни' 'не' 'ним' 'нім' 'ної' 'ну' 'ною' 'них' 'ні' 'ник' 'но' 'ного' 'ними' 'ному' // 'ній' many nouns ?
      'ього' 'ьому'
      'ьої' 'ьою'
      'іх' 'іми'
      'ова' 'ове'
      'їй' 'иїй'
      'йому' 'ийому'
      'єє' 'еє'
      'ен' 'ена' 'ені' 'ене' 'ену' 'енам' 'ени' 'енів' 'еном'
      'яча' 'яче' 'ячу' 'ячі'
      'ача' 'аче' 'ачу' 'ачі'
      'юча' 'юче' 'ючу' 'ючі'
      'уча' 'уче' 'учу' 'учі'
        (delete)

      'во'
        (v delete) // батато|во but абатство
    )
  )

  define adjectival as (
    adjective

    try (
      [substring] among (
        'ен' // зел|ен.ий
        'ов' // абетк|ов.ого
        'яч'
        'ач'
        'юч'
        'уч'
        (delete)
      )
    )
  )

  define verb as (
    [substring] among (
      'ав' 'али' 'ало' 'ала' 'ать' 'ати' // пізнав|ав
      'іть'
      'йте' 'ймо'
      'ме'
      'ла' 'ло' 'ли' 'лу'
      'но'
      //'ти' 'ть' 'те' // need to remove ?
      'єш' 'ємо' 'єте' 'ють'
      'еш' 'емо' 'ете' 'уть'
      'лю' 'иш' 'ить' 'имо' 'ите' 'лять' 'ши'
      'ляти'
      'їш' 'їть' 'їмо' 'їте' 'ять' 'яти'
        (delete)
    )
  )

  define noun as (
    [substring] among (
      'ам' 'ами' 'ах' // вод|ам
      'ар' 'арем' 'аря'
      'ею'
      'ям' 'ями' 'ях'
      'ові' 'ом'
      'ець' 'єць' 'ем'
      'ень'
      'ой'
      'ію' 'ії'
      'ів'
      'ев' 'ов' 'ей' 'иям' 'иях' 'ию'
      'іям' 'іях' 'ія'
      'ия' 'єю' 'еві' 'єм' 'еїв' 'їв'
      'ією' 'иєю' 'еєю'
      'ця' 'цю' // ? авіанос|ця  авіанос|цю
      'ка' 'ки' 'ці' 'ку' 'ко' 'ок' // female gender
      'ьї' 'ьє' 'ьєю' 'ью' 'ья'
      'ння' 'нням' 'ннями' 'нню' 'ннях'
        (delete)
    )
  )

  define remove_last_2_vowels as (
    remove_vowel_before_vowel
      and remove_vowel
  )

  define remove_vowel as (
    [substring] among (
      'а' 'е' 'є' 'и' 'і' 'ї'
      'й' 'о' 'у' 'ь' 'ю' 'я'
        (delete)
    )
  )

  define remove_vowel_before_vowel as (
    [substring] among (
      'а' 'е' 'є' 'и' 'і' 'ї' 'й' 'о' 'у' 'ь' 'ю' 'я'
      ('а' or 'е' or 'є' or 'и' or 'і' or 'ї' or 'й' or 'о' or 'у' or 'ь' or 'ю' or 'я' delete )
    )
  )

  // define remove_last_letter as (
  //   [substring] among (
  //     'а' 'е' 'є' 'и' 'і' 'ї'// 'в' // NOUN: вод|а, VERB: вчи|в
  //     'й' 'о' 'у' 'ь' 'ю' 'я'
  //       (delete)
  //   )
  // )

  define remove_letter_v as (
    [substring] among (
      'в'
        (v delete) // when any vowels before
        // ('а' or 'е' or 'і' or 'о' or 'у' delete)
        // ('а' or 'е' or 'и' or 'і' or 'о' or 'у' delete)
        // ( <- 'и')
        // (delete)
    )
  )

  define remove_last_2_letters as ( // 2-letters from the all previous sets
    [substring] among (
      'ав' 'ам' 'ар' 'ах' 'ач'
      'ев' 'еє' 'ей' 'ем' 'ен' 'еш' 'ею'
      'єє' 'єм' 'єш' 'єю'
      'ий' 'им' 'их' 'иш' 'ию' 'ия'
      'ів' 'їв' 'ії' 'ій' 'їй' 'ім' 'іх' 'ію'
      'їш'
      'ла' 'ли' 'ло' 'лю' // аку|лі ?
      'ме' // дія|ми ?
      'ов' 'ої' 'ой' 'ом' 'ою' 'сь' 'ся'
       //'ти' 'те' 'ть' // need to remove ?
      'уч'
      'ьє' 'ьї' 'ью' 'ья'
      'юч'
      'ям' 'ях' 'яч'
        (delete)
    )
  )

  define derivational as (
    [substring] R2 among (
      'іст' // незалежн|іст.тю
      'ост'
      'ень' 'еньк'
      'ськ'
      'ізм'
      'ізац'
        (delete)
    )
  )

  define tidy_up as (
    [substring] among (
      'б' ('б' delete) // хоб|б.і
      'в' ('в' delete)
      'г' ('г' delete)
      'д' ('д' delete) // усевлад|д.ям
      'ж' ('ж' delete)
      'з' ('з' delete)
      'к' ('к' delete)
      'л' ('л' delete)
      'м' ('м' delete)
      'н' ('н' delete)
      'п' ('п' delete)
      'р' ('р' delete)
      'с' ('с' delete)
      'т' ('т' delete)
      'ф' ('ф' delete)
      'ц' ('ц' delete)
      'ч' ('ч' delete)
      'ш' ('ш' delete)
    )
  )

  define min_len as (
    $(len >= 4)
  )

  define short_word_4l as (
    $(len == 4) and (remove_last_2_vowels or remove_vowel)
  )

  define short_word_5l as (
    $(len == 5) and (
      remove_last_2_vowels
        or remove_vowel
        or remove_last_2_letters
        or remove_letter_v
    ) or
      $(len == 5) and do remove_vowel
  )

  define long_word as (
    $(len > 5) and do (
      do (
        perfective_gerund or (
          try reflexive

          long_endings or adjectival or verb or noun or remove_vowel or remove_letter_v
        )
      )

      do derivational
      // do remove_letter_v
      do remove_last_2_vowels
      do remove_vowel
    )
  )
)

define stem as (
  do prelude
  do mark_regions

  backwards setlimit tomark pV for (
    min_len

    short_word_4l or short_word_5l or long_word

    do tidy_up
  )
)
